apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Internal")) {
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "io.numbersprotocol.starlingcapture"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 7
        versionName "1.7.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }

        buildConfigField("String", "NUMBERS_STORAGE_BASE_URL", "\"${System.env.NUMBERS_STORAGE_BASE_URL}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }

        qa {
            initWith debug
            applicationIdSuffix ".qa"
            versionNameSuffix "-qa"
        }
    }

    flavorDimensions "mode"
    productFlavors {
        master {
            dimension "mode"
            versionNameSuffix "-master"
        }
        internal {
            dimension "mode"
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    androidExtensions {
        experimental = true
    }

    lintOptions {
        warningsAsErrors true
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        // TODO: Enable the "allWarningAsErrors" after most  of the Kotlin dependencies are updated
        //       to 1.4.10. Currently, the following dependencies are not update-to-date.
        //       jetified-kotlin-reflect-1.4.0.jar (version 1.4)
        //       jetified-kotlin-stdlib-jdk7-1.3.72.jar (version 1.3)
        //       jetified-kotlin-stdlib-1.4.10.jar (version 1.4)
        //       jetified-kotlin-stdlib-common-1.4.10.jar (version 1.4)
        // kotlinOptions.allWarningsAsErrors = true
    }

    packagingOptions {
        // Solve INSTALL_FAILED_NO_MATCHING_ABIS error by ProofMode dependency.
        exclude "lib/x86_64/darwin/libscrypt.dylib"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9"

    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.2"
    implementation "androidx.exifinterface:exifinterface:1.3.0"
    implementation "androidx.paging:paging-runtime-ktx:2.1.2"
    implementation "androidx.work:work-runtime-ktx:2.4.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    implementation "androidx.navigation:navigation-fragment-ktx:2.3.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.0"

    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"

    implementation "com.google.android.material:material:1.3.0-alpha03"

    implementation "org.koin:koin-core:2.1.6"
    implementation "org.koin:koin-core-ext:2.1.6"
    implementation "org.koin:koin-androidx-viewmodel:2.1.6"
    implementation "org.koin:koin-androidx-fragment:2.1.6"
    testImplementation "org.koin:koin-test:2.1.6"

    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    implementation "com.squareup.moshi:moshi:1.11.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.11.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.11.0"

    implementation "com.afollestad.material-dialogs:core:3.3.0"
    implementation "com.afollestad.material-dialogs:lifecycle:3.3.0"
    implementation "com.afollestad.material-dialogs:input:3.3.0"
    implementation "com.afollestad.material-dialogs:bottomsheets:3.3.0"

    implementation "io.coil-kt:coil:0.13.0"
    implementation "io.coil-kt:coil-video:0.13.0"

    implementation "com.github.lisawray.groupie:groupie:2.8.1"
    implementation "com.github.lisawray.groupie:groupie-viewbinding:2.8.1"
    implementation "com.github.lisawray.groupie:groupie-kotlin-android-extensions:2.8.1"

    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.karumi:dexter:6.2.1"
    implementation "com.github.stfalcon:stfalcon-imageviewer:1.0.1"
    implementation "com.github.numbersprotocol:android-info-snapshot:0.4.1"
    implementation "com.otaliastudios:cameraview:2.6.4"
    implementation files("libs/zkma-3.4.6.aar")

    // Because android-libproofmode >= 0.6 will have duplicate classes: okio.* and we currently do
    // not know how to resolve it, we force the version of android-libproofmode to 0.0.4. Error
    // details: https://stackoverflow.com/questions/64300437/duplicate-class-okio-in-module-com-eternitywalljava-opentimestamps1-17-with-pr
    //noinspection GradleDependency
    implementation("org.witness:android-libproofmode:0.6") {
        exclude group: "com.google.guava", module: "guava"
    }

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.5"

    internalImplementation "com.google.firebase:firebase-analytics-ktx:17.6.0"
    internalImplementation "com.google.firebase:firebase-crashlytics:17.2.2"

    testImplementation "junit:junit:4.13.1"
    testImplementation "org.mockito:mockito-core:3.5.13"

    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
}

task printVersionCode {
    doLast {
        println(android.defaultConfig.versionCode)
    }
}

task printVersionName {
    doLast {
        println(android.defaultConfig.versionName)
    }
}